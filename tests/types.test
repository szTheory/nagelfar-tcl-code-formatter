#------------------------------------------------------------*- tcl -*-
# Tests for types
#----------------------------------------------------------------------
# $Revision$
#----------------------------------------------------------------------

test types-1.1 {
    Types checking
} -body {
    createTestFile {
        set apa [list x y z]
        llength $apa
    }
    execTestFile
} -result {%%}

test types-2.1 {
    Types in overwritten vars.
} -body {
    createTestFile {
        set apa [frame .f -padx 3 -pady 3]
        $apa configure -gurkmeja 2
        # A non-type does not overwrite
        set apa "Hej"
        $apa gurkmeja
        # A type might overwrite
        set apa [button .b]
        $apa configure -gurkmeja 2
    }
    execTestFile
} -result {%%Line   3: E Bad option -gurkmeja to "_obj,frame configure"
Line   6: E Unknown subcommand "gurkmeja" to "_obj,frame"
Line   9: E Bad option -gurkmeja to "_obj,button configure"}

test types-3.1 {
    Types in arrays
} -body {
    createTestFile {
        set w .t
        set data(sbar)   [scrollbar $w.sbar]
        set data(canvas) [canvas $w.canvas]
        $data(sbar) configure -command [list $data(canvas) xview]
        $data(canvas) configure -xscrollcommand [list $data(sbar) set]
    }
    execTestFile
} -result {%%}

test types-3.2 {
    Types in arrays
} -body {
    createTestFile {
        set x canvas
        set w .t
        set data(sbar)   [scrollbar $w.sbar]
        set data(canvas) [canvas $w.canvas]
        # There is no reasonable way to guess this type,
        # so this should be silent.
        $data($x) configureApa xxx
    }
    execTestFile
} -result {%%}

test types-3.3 {
    Types in arrays
} -body {
    createTestFile {
        set x canvas
        set w .t
        set data(sbar)   [scrollbar $w.sbar]
        set data(canvas) [text $w.canvas]
        # An explicit type declaration for an array is valid for the base.
        # Though it must be set after any inconsistent assignements were made.
        ##nagelfar vartype data _obj,canvas
        $data($x) configureApa xxx
    }
    execTestFile
} -result {%%Line   9: E Unknown subcommand "configureApa" to "_obj,canvas"}

test types-3.4 {
    Types in arrays
} -body {
    createTestFile {
        set w .t
        for {set t 0} {$t < 5} {incr t} {
            set data($t) [canvas $w.canvas$t]
        }
        for {set t 0} {$t < 5} {incr t} {
            # Since data() has a consistent type above this can be detected
            $data($t) configureApa xxx
        }
    }
    execTestFile
} -result {%%Line   8: E Unknown subcommand "configureApa" to "_obj,canvas"}

test types-4.1 {
    Types in inline comments
} -body {
    createTestFile {
        set apa [frame .f -padx 3 -pady 3]
        $apa configure -gurkmeja 2
        ##nagelfar variable apa varName
        set $apa
    }
    execTestFile
} -result {%%Line   3: E Bad option -gurkmeja to "_obj,frame configure"}

test types-4.2 {
    Types in inline comments
} -body {
    createTestFile {
        set apa [frame .f -padx 3 -pady 3]
        $apa configure -gurkmeja 2
        ##nagelfar vartype apa varName
        set $apa
    }
    execTestFile
} -result {%%Line   3: E Bad option -gurkmeja to "_obj,frame configure"}

test types-4.3 {
    Types in inline comments
} -body {
    createTestFile {
        # vartype cannot create the varaible
        ##nagelfar vartype apa varName
        list $apa
    }
    execTestFile
} -result {%%Line   3: E Unknown variable "apa"
Line   4: E Unknown variable "apa"}

test types-5.1 {
    Types, option
} -body {
    createTestFile {
        set lst "hej hopp"
        set apa -decreasing
        ##nagelfar vartype apa option
        lsort -real $apa $lst
        lsort $apa       $lst
        lsort $apa -real $lst
        # These should be detected
        lsort -real $apa $lst $lst
        lsort $apa       $lst $lst
        lsort $apa -real $lst $lst
    }
    execTestFile
} -result {%%Line   9: E Wrong number of arguments (4) to "lsort"
Line  10: E Wrong number of arguments (3) to "lsort"
Line  11: E Wrong number of arguments (4) to "lsort"}

test types-5.2 {
    Types, option
} -body {
    createTestFile {
        set lst "hej hopp"
        set apa -decreasing
        ##nagelfar vartype apa option
        lsort -real {*}$apa $lst
        lsort {*}$apa       $lst
        lsort {*}$apa -real $lst
        # These should be detected
        lsort -real {*}$apa $lst $lst
        lsort {*}$apa       $lst $lst
        lsort {*}$apa -real $lst $lst
        # These are ok. We consider expansion a valid pair.
        button .b {*}$lst
        ttk::style configure My.Toolbutton {*}[ttk::style configure Toolbutton] \
                -padding {1 1}
        ttk::style map My.Toolbutton {*}[ttk::style map Toolbutton] \
                -relief {disabled flat selected sunken pressed sunken active raised}
    }
    execTestFile
} -result {%%Line   9: E Wrong number of arguments (4) to "lsort"
Line  10: E Wrong number of arguments (3) to "lsort"
Line  11: E Wrong number of arguments (4) to "lsort"}
